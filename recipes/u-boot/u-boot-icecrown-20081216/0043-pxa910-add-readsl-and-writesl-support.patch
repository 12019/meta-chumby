From bfc9018ecc6e26108fdbd2bcbfe86786e8a1d0c4 Mon Sep 17 00:00:00 2001
From: Lei Wen <leiwen@marvell.com>
Date: Mon, 16 Mar 2009 10:25:38 +0800
Subject: [PATCH] pxa910: add readsl and writesl support

Signed-off-by: Lei Wen <leiwen@marvell.com>
---
 lib_arm/Makefile     |    2 +
 lib_arm/io-readsl.S  |   78 ++++++++++++++++++++++++++++++++++++++++++++++++++
 lib_arm/io-writesl.S |   66 ++++++++++++++++++++++++++++++++++++++++++
 3 files changed, 146 insertions(+), 0 deletions(-)
 create mode 100644 lib_arm/io-readsl.S
 create mode 100644 lib_arm/io-writesl.S

diff --git a/lib_arm/Makefile b/lib_arm/Makefile
index c8795b2..3a0ad2f 100644
--- a/lib_arm/Makefile
+++ b/lib_arm/Makefile
@@ -31,6 +31,8 @@ SOBJS-y	+= _divsi3.o
 SOBJS-y	+= _modsi3.o
 SOBJS-y	+= _udivsi3.o
 SOBJS-y	+= _umodsi3.o
+SOBJS-y	+= io-readsl.o
+SOBJS-y	+= io-writesl.o
 
 COBJS-y	+= board.o
 COBJS-y	+= bootm.o
diff --git a/lib_arm/io-readsl.S b/lib_arm/io-readsl.S
new file mode 100644
index 0000000..ecaea44
--- /dev/null
+++ b/lib_arm/io-readsl.S
@@ -0,0 +1,78 @@
+/*
+ *  linux/arch/arm/lib/io-readsl.S
+ *
+ *  Copyright (C) 1995-2000 Russell King
+ *
+ * This program is free software; you can redistribute it and/or modify
+ * it under the terms of the GNU General Public License version 2 as
+ * published by the Free Software Foundation.
+ */
+
+.global __raw_readsl
+
+__raw_readsl:
+		teq	r2, #0		@ do we have to check for the zero len?
+		moveq	pc, lr
+		ands	ip, r1, #3
+		bne	3f
+
+		subs	r2, r2, #4
+		bmi	2f
+		stmfd	sp!, {r4, lr}
+1:		ldr	r3, [r0, #0]
+		ldr	r4, [r0, #0]
+		ldr	ip, [r0, #0]
+		ldr	lr, [r0, #0]
+		subs	r2, r2, #4
+		stmia	r1!, {r3, r4, ip, lr}
+		bpl	1b
+		ldmfd	sp!, {r4, lr}
+2:		movs	r2, r2, lsl #31
+		ldrcs	r3, [r0, #0]
+		ldrcs	ip, [r0, #0]
+		stmcsia	r1!, {r3, ip}
+		ldrne	r3, [r0, #0]
+		strne	r3, [r1, #0]
+		mov	pc, lr
+
+3:		ldr	r3, [r0]
+		cmp	ip, #2
+		mov	ip, r3, lsl #0
+		strb	ip, [r1], #1
+		bgt	6f
+		mov	ip, r3, lsl #8
+		strb	ip, [r1], #1
+		beq	5f
+		mov	ip, r3, lsl #16
+		strb	ip, [r1], #1
+
+4:		subs	r2, r2, #1
+		mov	ip, r3, lsr #24
+		ldrne	r3, [r0]
+		orrne	ip, ip, r3, lsl #8
+		strne	ip, [r1], #4
+		bne	4b
+		b	8f
+
+5:		subs	r2, r2, #1
+		mov	ip, r3, lsr #16
+		ldrne	r3, [r0]
+		orrne	ip, ip, r3, lsl #16
+		strne	ip, [r1], #4
+		bne	5b
+		b	7f
+
+6:		subs	r2, r2, #1
+		mov	ip, r3, lsr #8
+		ldrne	r3, [r0]
+		orrne	ip, ip, r3, lsl #24
+		strne	ip, [r1], #4
+		bne	6b
+
+		mov	r3, ip, lsl #16
+		strb	r3, [r1, #2]
+7:		mov	r3, ip, lsl #8
+		strb	r3, [r1, #1]
+8:		mov	r3, ip, lsl #0
+		strb	r3, [r1, #0]
+		mov	pc, lr
diff --git a/lib_arm/io-writesl.S b/lib_arm/io-writesl.S
new file mode 100644
index 0000000..859d7de
--- /dev/null
+++ b/lib_arm/io-writesl.S
@@ -0,0 +1,66 @@
+/*
+ *  linux/arch/arm/lib/io-writesl.S
+ *
+ *  Copyright (C) 1995-2000 Russell King
+ *
+ * This program is free software; you can redistribute it and/or modify
+ * it under the terms of the GNU General Public License version 2 as
+ * published by the Free Software Foundation.
+ */
+
+.global __raw_writesl
+
+__raw_writesl:
+		teq	r2, #0		@ do we have to check for the zero len?
+		moveq	pc, lr
+		ands	ip, r1, #3
+		bne	3f
+
+		subs	r2, r2, #4
+		bmi	2f
+		stmfd	sp!, {r4, lr}
+1:		ldmia	r1!, {r3, r4, ip, lr}
+		subs	r2, r2, #4
+		str	r3, [r0, #0]
+		str	r4, [r0, #0]
+		str	ip, [r0, #0]
+		str	lr, [r0, #0]
+		bpl	1b
+		ldmfd	sp!, {r4, lr}
+2:		movs	r2, r2, lsl #31
+		ldmcsia	r1!, {r3, ip}
+		strcs	r3, [r0, #0]
+		ldrne	r3, [r1, #0]
+		strcs	ip, [r0, #0]
+		strne	r3, [r0, #0]
+		mov	pc, lr
+
+3:		bic	r1, r1, #3
+		ldr	r3, [r1], #4
+		cmp	ip, #2
+		blt	5f
+		bgt	6f
+
+4:		mov	ip, r3, lsr #16
+		ldr	r3, [r1], #4
+		subs	r2, r2, #1
+		orr	ip, ip, r3, lsl #16
+		str	ip, [r0]
+		bne	4b
+		mov	pc, lr
+
+5:		mov	ip, r3, lsr #8
+		ldr	r3, [r1], #4
+		subs	r2, r2, #1
+		orr	ip, ip, r3, lsl #24
+		str	ip, [r0]
+		bne	5b
+		mov	pc, lr
+
+6:		mov	ip, r3, lsr #24
+		ldr	r3, [r1], #4
+		subs	r2, r2, #1
+		orr	ip, ip, r3, lsl #8
+		str	ip, [r0]
+		bne	6b
+		mov	pc, lr
-- 
1.6.0.4

